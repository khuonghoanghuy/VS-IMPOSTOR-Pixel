package flixel.input.touch;

#if FLX_TOUCH
import flixel.FlxG;
import flixel.input.FlxInput;
import flixel.input.FlxSwipe;
import flixel.input.IFlxInput;
import flixel.math.FlxPoint;
import flixel.util.FlxDestroyUtil;

import openfl.geom.Point;

/**
 * Helper class, contains and tracks touch points in your game.
 * Automatically accounts for parallax scrolling, etc.
 */
@:allow(flixel.input.touch.FlxTouchManager)
class FlxTouch extends FlxPointer implements IFlxDestroyable implements IFlxInput
{
	/**
	 * The _unique_ ID of this touch. You should not make not any further assumptions
	 * about this value - IDs are not guaranteed to start from 0 or ascend in order.
	 * The behavior may vary from device to device.
	 */
	public var touchPointID(get, never):Int;

	/**
	 * Check to see if the mouse has just been moved.
	 */
	public var justMoved(get, never):Bool;

	/**
	 * Check to see if the mouse just moved leftwards.
	 */
	public var justMovedLeft(get, never):Bool;

	/**
	 * Check to see if the mouse just moved rightwards.
	 */
	public var justMovedRight(get, never):Bool;

	/**
	 * Check to see if the mouse just moved upwards.
	 */
	public var justMovedUp(get, never):Bool;

	/**
	 * Check to see if the mouse just moved downwards.
	 */
	public var justMovedDown(get, never):Bool;

	/**
	 * Distance in pixels the mouse has moved since the last frame in the X direction.
	 */
	public var deltaX(get, never):Int;

	/**
	 * Distance in pixels the mouse has moved since the last frame in the Y direction.
	 */
	public var deltaY(get, never):Int;

	/**
	 * Distance in pixels this touch has moved in view space since the last frame in the X direction.
	 */
	public var deltaViewX(get, default):Float;

	/**
	 * Distance in pixels this touch has moved in view space since the last frame in the Y direction.
	 */
	public var deltaViewY(get, default):Float;

	/**
	 * A value between 0.0 and 1.0 indicating force of the contact with the device. If the device does not support detecting the pressure, the value is 1.0.
	 */
	public var pressure(default, null):Float;

	/**
	 * Check to see if this touch has just been pressed.
	 */
	public var justPressed(get, never):Bool;

	/**
	 * Check to see if this touch is currently pressed.
	 */
	public var pressed(get, never):Bool;

	/**
	 * Check to see if this touch has just been released.
	 */
	public var justReleased(get, never):Bool;

	/**
	 * Check to see if this touch is currently not pressed.
	 */
	public var released(get, never):Bool;

	/**
	 * The velocity of the mouse.
	 */
	public var velocity(default, null):FlxPoint = FlxPoint.get();

	var input:FlxInput<Int>;
	var flashPoint = new Point();

	public var justPressedPosition(default, null) = FlxPoint.get();
	public var justPressedTimeInTicks(default, null):Int = -1;

	var _prevX:Int = 0;
	var _prevY:Int = 0;
	var _prevViewX:Int = 0;
	var _prevViewY:Int = 0;
	var _startX:Int = 0;
	var _startY:Int = 0;
	var _swipeDeltaX(get, never):Float;
	var _swipeDeltaY(get, never):Float;

	public function destroy():Void
	{
		input = null;
		justPressedPosition = FlxDestroyUtil.put(justPressedPosition);
		velocity = FlxDestroyUtil.put(velocity);
		flashPoint = null;

		velocity = FlxDestroyUtil.put(velocity);
	}

	/**
	 * Resets the justPressed/justReleased flags, sets touch to not pressed and sets touch pressure to 0.
	 */
	public function recycle(x:Int, y:Int, pointID:Int, pressure:Float):Void
	{
		setXY(x, y);
		input.ID = pointID;
		input.reset();
		this.pressure = pressure;
	}

	/**
	 * @param	X			stageX touch coordinate
	 * @param	Y			stageX touch coordinate
	 * @param	PointID		touchPointID of the touch
	 * @param	pressure	A value between 0.0 and 1.0 indicating force of the contact with the device. If the device does not support detecting the pressure, the value is 1.0.
	 */
	function new(x:Int = 0, y:Int = 0, pointID:Int = 0, pressure:Float = 0)
	{
		super();

		input = new FlxInput(pointID);
		setXY(x, y);
		this.pressure = pressure;
	}

	/**
	 * Called by the internal game loop to update the just pressed/just released flags.
	 */
	function update():Void
	{
		input.update();

		if (justPressed)
		{
			justPressedPosition.set(viewX, viewY);
			justPressedTimeInTicks = FlxG.game.ticks;
			_startX = viewX;
			_startY = viewY;
		}
		#if FLX_POINTER_INPUT
		else if (justReleased)
		{
			FlxG.touches.flickManager.initFlick(velocity, touchPointID);
			FlxG.swipes.push(new FlxSwipe(touchPointID, justPressedPosition.copyTo(), getViewPosition(), justPressedTimeInTicks));
		}
		if (pressed)
		{
			FlxG.touches.flickManager.destroy();
		}
		#end
	}

	/**
	 * Function for updating touch coordinates. Called by the TouchManager.
	 *
	 * @param	X	stageX touch coordinate
	 * @param	Y	stageY touch coordinate
	 */
	function setXY(X:Int, Y:Int, updatePrev:Bool = false):Void
	{
		calculateVelocity();

		if (!updatePrev)
		{
			_prevX = x;
			_prevY = y;
			_prevViewX = viewX;
			_prevViewY = viewY;
		}

		flashPoint.setTo(X, Y);
		flashPoint = FlxG.game.globalToLocal(flashPoint);

		setRawPositionUnsafe(flashPoint.x, flashPoint.y);

		if (updatePrev)
		{
			_prevX = x;
			_prevY = y;
			_prevViewX = viewX;
			_prevViewY = viewY;
		}
	}

	function calculateVelocity():Void
	{
		if (!pressed)
			return;

		velocity.x = deltaViewX;
		velocity.y = deltaViewY;
	}

	inline function get_touchPointID():Int
	{
		return input.ID;
	}

	inline function get_justMoved():Bool
		return _prevX != x || _prevY != y;

	function get_justMovedLeft():Bool
	{
		var swiped:Bool = _swipeDeltaX < -FlxG.touches.swipeThreshold.x;
		if (swiped)
			_startX = viewX;
		return swiped;
	}

	function get_justMovedRight():Bool
	{
		var swiped:Bool = _swipeDeltaX > FlxG.touches.swipeThreshold.x;
		if (swiped)
			_startX = viewX;
		return swiped;
	}

	function get_justMovedUp():Bool
	{
		var swiped:Bool = _swipeDeltaY < -FlxG.touches.swipeThreshold.y;
		if (swiped)
			_startY = viewY;
		return swiped;
	}

	function get_justMovedDown():Bool
	{
		var swiped:Bool = _swipeDeltaY > FlxG.touches.swipeThreshold.y;
		if (swiped)
			_startY = viewY;
		return swiped;
	}

	inline function get_deltaX():Int
		return x - _prevX;

	inline function get_deltaY():Int
		return y - _prevY;

	inline function get_deltaViewX():Float
		return viewX - _prevViewX;

	inline function get_deltaViewY():Float
		return viewY - _prevViewY;

	inline function get__swipeDeltaX():Float
		return viewX - _startX;

	inline function get__swipeDeltaY():Float
		return viewY - _startY;

	inline function get_justReleased():Bool
		return input.justReleased;

	inline function get_released():Bool
		return input.released;

	inline function get_pressed():Bool
		return input.pressed;

	inline function get_justPressed():Bool
		return input.justPressed;
}
#else
class FlxTouch {}
#end
